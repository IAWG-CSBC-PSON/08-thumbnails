from os.path import join

SRC_DIR = "src"
DATA_DIR = "data"
RAW_DIR = join(DATA_DIR, "raw")
PROCESSED_DIR = join(DATA_DIR, "processed")
DEBUG_DIR = join(DATA_DIR, "debug")

DOWNLOAD_DIR = join(RAW_DIR, "download")

FILENAME_TO_SYN_ID = {
   "cycif_tonsil.ome.tif": "syn26946496",
   "cycif_tonsil_small.tiff": "syn27108519",
   "mibi_liver.ome.tiff": "syn26858183",
   "mibi_placenta.ome.tiff": "syn26858168",
   "mibi_thymus.ome.tiff": "syn26858167",
   "mibi_tonsil.ome.tiff": "syn26858166",
   "mibi_tumor_FOV1.ome.tiff": "syn26858194",
   "mibi_tumor_FOV3.ome.tiff": "syn26858193",
   "mibi_tumor_FOV5.ome.tiff": "syn26858192",
}

COLOR_ASSIGNMENT_METHODS = [
    "pixel_sum",
    "pixel_max",
    "group_hue"
]

rule all:
    input:
        expand(
            join(PROCESSED_DIR, "{filename}.{num_colors}.{color_method}.thumbnail.jpg"),
            filename=FILENAME_TO_SYN_ID.keys(),
            num_colors=range(3, 7),
            color_method=COLOR_ASSIGNMENT_METHODS
        ),
        expand(
            join(DEBUG_DIR, "{filename}.{num_colors}.{color_method}.{color_index}.thumbnail.jpg"),
            filename=["cycif_tonsil.ome.tif"],
            num_colors=[3],
            color_method=["pixel_sum"],
            color_index=[1, 2, 3]
        )

# Debugging outputs
rule run_miniature_hclust_split_channels:
    input:
        join(DOWNLOAD_DIR, "{filename}")
    output:
        thumbnail=join(DEBUG_DIR, "{filename}.{num_colors}.{color_method}.{color_index}.thumbnail.jpg")
    shell:
        """
        python ../../miniature/paint_miniature.py \
            '{input}' \
            '{output.thumbnail}' \
            --remove_bg False \
            --dimred hclust \
            --num_colors {wildcards.num_colors} \
            --color_method {wildcards.color_method} \
            --color_index {wildcards.color_index}
        """

# Regular outputs
rule run_miniature_hclust:
    input:
        join(DOWNLOAD_DIR, "{filename}")
    output:
        thumbnail=join(PROCESSED_DIR, "{filename}.{num_colors}.{color_method}.thumbnail.jpg"),
        dendrogram=join(PROCESSED_DIR, "{filename}.{num_colors}.{color_method}.dendrogram.jpg")
    shell:
        """
        python ../../miniature/paint_miniature.py \
            '{input}' \
            '{output.thumbnail}' \
            --remove_bg False \
            --dimred hclust \
            --num_colors {wildcards.num_colors} \
            --color_method {wildcards.color_method} \
            --dendrogram_file {output.dendrogram}
        """

rule download_data:
    output:
        join(DOWNLOAD_DIR, "{filename}")
    params:
        script=join(SRC_DIR, "download_file_from_synapse.py"),
        synapse_id=(lambda w: FILENAME_TO_SYN_ID[w.filename])
    shell:
        """
        python {params.script} \
            --synapse_id {params.synapse_id} \
            --output_dir {DOWNLOAD_DIR}
        """
